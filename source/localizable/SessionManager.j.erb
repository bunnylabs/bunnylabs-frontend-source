@import <Foundation/Foundation.j>

@import "SessionWindow.j"
@import "SessionMenuItem.j"
@import "DesktopManager.j"

@import "Utils/HashFragment.j"
@import "Utils/URLQueryString.j"

var GITHUB_CLIENT_ID = "<%= config[:github_client_id] %>";
var apiServerUrl = "<%= config[:api_server_url] %>";
var session;

NOTIFICATION_LOGIN_DATA_UPDATED = "NOTIFICATION_LOGIN_DATA_UPDATED";

function popupwindow(url, title, w, h) {
  var left = (screen.width/2)-(w/2);
  var top = (screen.height/2)-(h/2);
  return window.open(url, title, 'toolbar=no, location=no, directories=no, status=no, menubar=no, scrollbars=no, resizable=no, copyhistory=no, width='+w+', height='+h+', top='+top+', left='+left);
}

@implementation SessionManager : CPViewController
{
    SEL currentResponseHandler;
    int currentStatusCode;
    CPString currentData;

    SessionMenuItem statusMenuItem;

    id userData;
    id persistentStore;
}

-(BOOL)acceptsFirstResponder
{
    return YES;
}


-(id)init
{
	self = [super init];
	if (self)
	{
        persistentStore = new Persist.Store("<%= config[:persistent_store_name] %>");
        [self setView:[[SessionWindow alloc] init]];
        [[self view] setDelegate:self];
        currentResponseHandler = @selector(noResponseHandler:data:)
        statusMenuItem = [[SessionMenuItem alloc] init];
        [statusMenuItem setTarget:self];
        [self getUserData];

        window.addEventListener('message', function (event) {
            try
            {
                var obj = [URLQueryString deserialize:event.data];
                [self handleSessionManagerMessage:obj];
            }
            catch (e)
            {
                CPLog("Received bad message");
            }
        });
	}
	return self;
}

-(CPString)authToken
{
    var token = persistentStore.get("authToken");
    if (!token)
    {
        return "";
    }
    return token;
}

-(void)setAuthToken:(CPString)aToken
{
    persistentStore.set("authToken", aToken);
}

-(void)clearAuthToken
{
    persistentStore.set("authToken", "");
}

-(void)desktopDidResizeToRect:(CGRect)newRect
{
    [[self view] updateForScreenWidth:newRect.size.width];
}

-(void)handleSessionManagerMessage:(id)anObject
{
    console.log(anObject);
    if (anObject.type === "githubLogin")
    {
        if (anObject.authToken)
        {
            [self setAuthToken:anObject.authToken];
            [self getUserData];
        }
        else if (anObject.error)
        {
            [self showLoginWindow];
            [[self view] setCustomError:[CPString stringWithFormat:"<%= t(:unable_to_github_login) %>", anObject.error]];
            [[self view] update];
        }
        else
        {
            console.log("wtf");
        }
    }

    // TODO: link accounts
}

-(id)menuItemClicked:(id)sender
{
    [self showLoginWindow];
    return sender;
}

-(void)getUserData
{
    if ([self authToken].length > 0)
    {
        currentResponseHandler = @selector(getUserDataHandler:forData:);
        [self get:"/sessions/current" andNotify:self];
    }
    else
    {
        userData = nil;
        [statusMenuItem setTitle:"<%= t(:login) %>"];
        [[CPNotificationCenter defaultCenter] postNotificationName:NOTIFICATION_LOGIN_DATA_UPDATED object:self];
    }
}

-(BOOL)isAdmin
{
    if (!userData)
    {
        return NO;
    }

    if (!userData.adminToken)
    {
        return NO;
    }

    return YES;
}

-(BOOL)isLoggedIn
{
    if (!userData)
    {
        return NO;
    }

    if (!userData.username)
    {
        return NO;
    }

    return YES;
}

-(void)nullResponseHandler:(int)statusCode forData:(CPString)data
{
    CPLog("Null response handler called");
}

-(void)getUserDataHandler:(int)statusCode forData:(CPString)data
{
    if (statusCode == 200)
    {
        var result = JSON.parse(data);
        [statusMenuItem setTitle:[CPString stringWithFormat:"<%= t(:logged_in_as) %>", result.username]];
        userData = result;
    }
    else
    {
        [[self view] setState:[SessionWindow loginState] andResetFields:NO];
        [[self view] setCustomError:data];
        [[self view] update];
        userData = nil;
    }

    [[CPNotificationCenter defaultCenter] postNotificationName:NOTIFICATION_LOGIN_DATA_UPDATED object:self];
}

-(void)validationResponseHandler:(int)statusCode forData:(CPString)data
{
    if (statusCode == 200)
    {
        var alert = [CPAlert alertWithError: [CPString stringWithFormat:"<%= t(:validation_pass) %>"]];
        [alert setAlertStyle:CPInformationalAlertStyle];
        [alert runModal];
    }
    else
    {
        var alert = [CPAlert alertWithError: [CPString stringWithFormat:"<%= t(:validation_fail) %>"]];
        [alert runModal];
    }
}

-(void)loginResponseHandler:(int)statusCode forData:(CPString)data
{
    if (statusCode == 200)
    {
        [self setAuthToken:data];
        [[DesktopManager instance] removeViewController:self];
    }
    else
    {
        [self clearAuthToken];
        [[self view] setState:[SessionWindow loginState] andResetFields:NO];
        [[self view] setCustomError:data];
        [[self view] update];
    }
    [self getUserData];
}

-(void)registerResponseHandler:(int)statusCode forData:(CPString)data
{
    if (statusCode == 200)
    {
        [[self view] setState:[SessionWindow loginState]];
        [[self view] setCustomMessage:"<%= t(:registration_ok) %>"];
        [[self view] update];
    }
    else
    {
        [[self view] setState:[SessionWindow registrationState] andResetFields:NO];
        [[self view] setCustomError:data];
        [[self view] update];
    }
}

-(void)submitUsernameResponseHandler:(int)statusCode forData:(CPString)data
{
    if (statusCode == 200)
    {
        [[self view] setState:[SessionWindow loginState]];
        [[self view] setCustomMessage:"<%= t(:change_password_ok) %>"];
        [[self view] update];
    }
    else
    {
        [[self view] setState:[SessionWindow changePasswordState] andResetFields:NO];
        [[self view] setCustomError:data];
        [[self view] update];
    }
}

-(void)forgotPasswordResponseHandler:(int)statusCode forData:(CPString)data
{
    if (statusCode == 200)
    {
        [[self view] setState:[SessionWindow loginState]];
        [[self view] setCustomMessage:"<%= t(:forgot_password_ok) %>"];
        [[self view] update];
    }
    else
    {
        [[self view] setState:[SessionWindow forgotPasswordState] andResetFields:NO];
        [[self view] setCustomError:data];
        [[self view] update];
    }
}

-(CPURL)_buildUrl:(CPString)apiPath
{
    var query = {
        auth: [self authToken],
        lang: "<%= I18n.locale %>"
    };

    var url = [CPURL URLWithString:[CPString stringWithFormat:"%@%@?%@", apiServerUrl, apiPath, [URLQueryString serialize:query]]];

    return url;
}

-(void)_performRequest:(CPURLRequest)aRequest withDelegate:(id)delegate
{
    var conn = [[CPURLConnection alloc] initWithRequest:aRequest delegate:delegate];
    [conn start];
}

-(id)get:(CPString)aUrl andNotify:(id)delegate
{
    var url = [self _buildUrl:aUrl];
    var request = [CPURLRequest requestWithURL:url];
    [request setHTTPMethod:"GET"];
    [self _performRequest:request withDelegate:delegate];
}

-(id)post:(CPString)aUrl withData:(id)json andNotify:(id)delegate
{
    var url = [self _buildUrl:aUrl];
    var request = [CPURLRequest requestWithURL:url];
    [request setHTTPMethod:"POST"];
    [request setHTTPBody:JSON.stringify(json)];
    [self _performRequest:request withDelegate:delegate];
}

-(id)put:(CPString)aUrl withData:(id)json andNotify:(id)delegate
{
    var url = [self _buildUrl:aUrl];
    var request = [CPURLRequest requestWithURL:url];
    [request setHTTPMethod:"PUT"];
    [request setHTTPBody:JSON.stringify(json)];
    [self _performRequest:request withDelegate:delegate];
}

-(id)delete:(CPString)aUrl andNotify:(id)delegate
{
    var url = [self _buildUrl:aUrl];
    var request = [CPURLRequest requestWithURL:url];
    [request setHTTPMethod:"DELETE"];
    [self _performRequest:request withDelegate:delegate];
}

-(void)loginWithUsername:(CPString)aUsername andPassword:(CPString)aPassword
{
    CPLog("Login: %@ %@", aUsername, aPassword);
    currentResponseHandler = @selector(loginResponseHandler:forData:)
	[self post:"/sessions" withData:{username: aUsername, password: aPassword /* captcha */} andNotify:self];
}

-(void)register:(CPString)aUsername withPassword:(CPString)aPassword andEmail:(CPString)anEmail
{
    CPLog("Register: %@ %@ %@", aUsername, aPassword, anEmail);
    currentResponseHandler = @selector(registerResponseHandler:forData:)
	[self post:"/users" withData:{username: aUsername, password: aPassword, email: anEmail /* captcha */} andNotify:self];
}

-(void)changePassword:(CPString)aPassword forUser:(CPString)aUsername
{
    var hash = [HashFragment fragmentAsObject];

    CPLog("ChangePassword: %@ %@ %@", hash.validateUsername, aPassword, hash.forgotPassword);
    currentResponseHandler = @selector(submitUsernameResponseHandler:forData:)
	[self post:"/users/" + hash.validateUsername + "/password" withData:{
                    name: hash.validateUsername,
                password: aPassword,
         validationToken: hash.forgotPassword

    } andNotify:self];
}

-(void)requestForgottenPasswordForUser:(CPString)aUsername andEmail:(CPString)anEmail
{
    CPLog("ForgotPassword: %@ %@", aUsername, anEmail);
    currentResponseHandler = @selector(forgotPasswordResponseHandler:forData:)
	[self post:"/users/" + aUsername + "/forgotPassword" withData:{
            name:aUsername,
           email:anEmail
    } andNotify:self];
}

-(void)validateUser:(CPString)aUsername withToken:(CPString)aToken
{
    CPLog("Validating User: %@ %@", aUsername, aToken);
    currentResponseHandler = @selector(validationResponseHandler:forData:)
    [self post:"/users/" + aUsername + "/validation" withData:{
                   name: aUsername,
        validationToken: aToken
    } andNotify:self];
    [HashFragment clearFragment];
}

-(void)logout
{
    CPLog("Logout");
    currentResponseHandler = @selector(nullResponseHandler:forData:)
    [self delete:"/sessions/current" andNotify:self];
    [self clearAuthToken];
    [self getUserData];
}

-(void)connection:(CPURLConnection)connection didFailWithError:(id)error
{
    CPLog("Error: %@", error);
}

-(void)connection:(CPURLConnection)connection didReceiveResponse:(CPHTTPURLResponse)response
{
    currentStatusCode = [response statusCode];
}

-(void)connection:(CPURLConnection)connection didReceiveData:(CPString)data
{
    currentData = data;
}

-(void)connectionDidFinishLoading:(CPURLConnection)connection
{
    // hold down errors
    if (currentData.length > 200)
    {
        currentData = "<%= t(:internal_server_error) %>";
    }
    [self performSelector:currentResponseHandler withObject:currentStatusCode withObject:currentData];
}

-(void)connectionDidReceiveAuthenticationChallenge:(id)connection
{
    [self clearAuthToken];
    [self getUserData];
    [self showLoginWindow];
    [[self view] setCustomMessage:"<%= t(:login_required) %>"];
    [[self view] update];
}

-(void)loginWithGithub
{
    var queryString = [URLQueryString serialize:
    {
        client_id: GITHUB_CLIENT_ID,
            scope: "gist,user:email"
    }];

    var request = [CPString stringWithFormat:"https://github.com/login/oauth/authorize?%@", queryString];

    popupwindow(request, "asd", 1024, 768);

}

-(void)showLoginWindow
{
    if ([self authToken].length == 0)
    {
        [[DesktopManager instance] pushTopViewController:self];
        [[self view] setState:[SessionWindow loginState]];
    }
    else
    {
        [[DesktopManager instance] pushTopViewController:self];
        [[self view] setState:[SessionWindow logoutState]];
    }
}

-(void)showChangePasswordWindow
{
    [[DesktopManager instance] pushTopViewController:self];
    [[self view] setState:[SessionWindow changePasswordState]];
    [HashFragment clearFragment];
}

-(id)cancelButtonClicked:(id)sender
{
    [[DesktopManager instance] removeViewController:self];
}

-(id)loginButtonClicked:(id)sender
{
    [self loginWithUsername:[[self view] username] andPassword:[[self view] password]];
    [[self view] setState:[SessionWindow waitingState]];
}

-(id)logoutButtonClicked:(id)sender
{
    [self logout];
    [[DesktopManager instance] removeViewController:self];
}

-(id)registerButtonClicked:(id)sender
{
    [self register:[[self view] username] withPassword:[[self view] password] andEmail:[[self view] email]];
    [[self view] setState:[SessionWindow waitingState]];
}

-(id)submitUsernameButtonClicked:(id)sender
{
    [self requestForgottenPasswordForUser:[[self view] username] andEmail:[[self view] email]];
    [[self view] setState:[SessionWindow waitingState]];
}

-(id)changePasswordButtonClicked:(id)sender
{
    [self changePassword:[[self view] password] forUser:[[self view] username]];
    [[self view] setState:[SessionWindow waitingState]];
}

-(SessionMenuItem)sessionStatusMenuItem
{
    return statusMenuItem;
}

+(SessionManager)instance
{
	if (!session)
	{
		session = [[SessionManager alloc] init];
        [CPURLConnection setClassDelegate:session];
	}
	return session;
}

@end

